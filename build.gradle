plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

group = 'org.mobiami'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // Selenium and WebDriverManager
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.28.0'
    testImplementation 'io.github.bonigarcia:webdrivermanager:5.8.0'

    // Cucumber
    testImplementation 'io.cucumber:cucumber-java:7.20.0'
    testImplementation 'io.cucumber:cucumber-junit:7.20.0'

    // JUnit
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'

    //additonal
    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '5.5.0'
    testImplementation group: 'org.json', name: 'json', version: '20240303'
    testImplementation group: 'io.rest-assured', name: 'json-schema-validator', version: '5.5.0'
}

test {
    useJUnit() // Use JUnit 4
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

def tags = (findProperty('tags') ?: 'not @exclude')

tasks.register('cucumber', JavaExec) {
    description = "Run Cucumber tests"
    group = "verification"

    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
    mainClass.set("io.cucumber.core.cli.Main")
    args = [
            '--plugin', 'pretty',
            '--plugin', 'html:reports/test-report.html',
            '--glue', 'com.fahmi',
            '--tags', tags,
            'src/test/resources'
    ]
    dependsOn testClasses
}